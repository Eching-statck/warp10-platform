//
//   Copyright 2018-2020  SenX S.A.S.
//
//   Licensed under the Apache License, Version 2.0 (the "License");
//   you may not use this file except in compliance with the License.
//   You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
//   Unless required by applicable law or agreed to in writing, software
//   distributed under the License is distributed on an "AS IS" BASIS,
//   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//   See the License for the specific language governing permissions and
//   limitations under the License.
//

plugins {
    id "com.jfrog.bintray" version "1.8.4" apply false // Not applied to root, only to sub projects
}

//
// Update REVISION File
//
task updateRevision {
    doLast {
        File versionFile = new File(rootProject.projectDir, "warp10/src/main/resources/REVISION")
        versionFile.write(project.version)
    }
}

//
// Check git repo status and used properties to be compatible with publication
//
task checkPublicationState {
    doLast {
        //
        // Check all files are commited
        //
        def stdout = new ByteArrayOutputStream()
        exec {
            // Check for uncommited as well as untracked files.
            commandLine 'git', 'status', '--porcelain'
            standardOutput = stdout
        }

        if (0 != stdout.toString().trim().size()) {
            throw new GradleException("Cannot upload archive: local repo contains uncommited files (git status)")
        }

        //
        // Check nogit property is not used
        //
        if (null != System.getProperty("nogit")) {
            throw new GradleException("Cannot upload archive: -Dnogit cannot be used.")
        }
    }
}
// Get the version once
def versionFromGit = getVersionFromGit()

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    version = versionFromGit
    group = 'io.warp10'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    //
    // Repositories for dependency resolution for all subprojects
    //
    repositories {
        jcenter()
        maven {
            url 'https://repository.apache.org/content/groups/public'
        }
        mavenCentral()
        mavenLocal()
        maven {
            url 'https://dl.bintray.com/senx/maven'
        }
        maven {
            url 'https://dl.bintray.com/hbs/maven'
        }
    }

    //
    // Force versions
    //
    configurations.all {
        resolutionStrategy {
            // fail eagerly on version conflict (includes transitive dependencies)
            // e.g. multiple different versions of the same dependency (group and name are equal)
            //failOnVersionConflict()

            // force certain versions of dependencies (including transitive)
            //  *append new forced modules:
            // This is needed to force Guava to 15.0 and not 17.0 as Renjin pulls
            // HBase does not work with > 15.0
            force 'com.google.guava:guava:15.0'
        }
    }

    //
    // Compile options
    //
    compileJava {
        if (null != System.getProperty("nojvmDebug")) {
            options.compilerArgs << '-g:none'
        } else {
            options.compilerArgs << '-g'
        }

        //options.debug = false;
        //options.optimize = false;
        //options.debugOptions.debugLevel = ''
    }

    //
    // IDE CONFIGURATIONS 
    //
    // IDEA DEFAULT CONFIGURATION
    idea {
        module {
            inheritOutputDirs = true
        }
    }
    // ECLIPSE
    eclipse {
        classpath {
            //customizing the classes output directory:
            defaultOutputDir = file('build-eclipse')

            //default settings for downloading sources and Javadoc:
            downloadSources = true
            downloadJavadoc = false
        }
    }

    //
    // Generate thrift classes, used by token and warp10.
    //
    task generateThrift {
        doLast {
            def thriftOutputDir = new File(project.projectDir, 'src/generated/thrift')

            // Delete the thrift dir if exists
            if (thriftOutputDir.exists()) {
                thriftOutputDir.deleteDir()
            }
            thriftOutputDir.mkdirs()

            def thriftFiles = fileTree(dir: 'src/main/thrift').matching { include '**/*.thrift' }
            thriftFiles.collect {
                def file = relativePath(it)
                exec {
                    executable = (null != System.getenv('THRIFT_HOME')) ? System.getenv('THRIFT_HOME') + '/bin/thrift' : 'thrift'
                    args = ['--gen', 'java:private-members', '-I', '.', '-o', thriftOutputDir, file]
                }
            }
        }
    }

    //
    // Sources jar
    //
    task sourcesJar(type: Jar) {
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }

    //
    // Common jar conf
    //
    tasks.withType(Jar) {
        zip64 = true

        manifest {
            attributes(
                    // "Implementation-Title" to be set by each sub-project
                    "Implementation-Vendor": "Warp 10",
                    "Implementation-Version": project.version)
        }
    }

    //
    // Disable the new gradle metadata generation because it is not uploaded to bintray by the plugin
    //
    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    //
    // Default publishing conf for subprojects publishing on a maven repo
    //
    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
    }

    //
    // Bintray deployment common conf
    //
    bintray {
        user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

        publish = true
//        dryRun = true

        // publications (for maven repo) or filesSpec (for generic repo) to be set by each subproject.

        pkg {
            // repo and name to be set by each subproject
            userOrg = 'senx'
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/senx/warp10-platform.git'
            version {
                name = project.version
                released = new Date()
                vcsTag = project.version
            }
        }
    }

    build.dependsOn updateRevision
    bintrayUpload.dependsOn checkPublicationState
}

// ------------------------------------------------------------------
//                    AUTO RELEASE MANAGEMENT
// Gets the version name from the latest Git tag
// if no commit occurs -> the name of the tag likes 0.0.1
// if commit occurs -> 0.0.1-12-aabb1122 (number of commits + version number)
// ------------------------------------------------------------------
def getVersionFromGit() {
    def nogit = System.getProperty("nogit")
    if (null != nogit) {
        // Override version with the property value. To remove the version altogether use '-Dnogit' without value.
        return nogit
    } else {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--always'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
}


